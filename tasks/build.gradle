buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${version_spring_boot}")
    }
}

plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


group = 'de.korten'
version = '1.0.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile(
            "org.objenesis:objenesis:${version_objenesis}",
            "org.apache.wicket:wicket-core:${version_wicket}",
            "org.apache.wicket:wicket-spring:${version_wicket}",
            "org.wicketstuff:wicketstuff-annotation:${version_wicket}",
            "org.wicketstuff:wicketstuff-lambda-components:${version_wicket}",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-data-jpa",
            "org.flywaydb:flyway-core:${version_flyway}",
    )

    runtime(
            "com.h2database:h2:${version_h2}",
    )

    testCompile(
            "org.flywaydb.flyway-test-extensions:flyway-spring-test:${version_flyway_spring_test}",
            "org.springframework.boot:spring-boot-starter-test",
    )
}

task webpack(type: NodeTask) {
    inputs.dir("src/main/webapp/js")
    outputs.dir("src/main/webapp/dist/js")

    script = file("$projectDir/node_modules/.bin/webpack")
    args = ["src/main/webapp/js/index.js", "-o", "src/main/webapp/dist/js/main.js"]
}

task nodeSass(type: NodeTask) {
    inputs.dir("src/main/webapp/scss")
    outputs.dir("src/main/webapp/dist/css")

    script = file("$projectDir/node_modules/.bin/node-sass")
    args = ["src/main/webapp/scss/main.scss", "src/main/webapp/dist/css/main.css"]
}

webpack.dependsOn npmInstall
nodeSass.dependsOn npmInstall

bootRun.dependsOn(webpack, nodeSass)